<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:my="*"
			   height = "140" width="100%"
			   minWidth="300" minHeight="100" 
			   backgroundColor="#293134"
			   resize="application1_resizeHandler(event)" 
			   applicationComplete="app_complete(event)" >

	<fx:Script>
		<![CDATA[
			import org.osmf.events.TimeEvent;
			use namespace mx_internal;
			
			import flash.system.*;
			
			import mx.controls.*;
			import mx.core.mx_internal;
			import mx.events.*;
			import mx.managers.*;

			
			public var core:LocachaCore;
			
			public var camera:Camera;
			public var mic:Microphone;
			
			public var localDisplay:UserDisplay;
			public var startVideoButton:Button;
			
			public var userCams:Object = new Object();
			public var lastHeight:Number;
			
			public var sampleTimer:Timer;
			public var lastSample:Number;
			public var sendSamples:Array = new Array();
			
			
			protected function app_complete(event:FlexEvent):void
			{
				Security.allowDomain("*")
					
				core = new LocachaCore(this);
				
				core.connect();
				
				showStartVideoButton();
				
				if (ExternalInterface.available)
				{
					ExternalInterface.addCallback("turnOn", turnOn); 
					ExternalInterface.addCallback("turnOff", turnOff); 
					ExternalInterface.addCallback("selectUser", selectUser); 
				} 

				sampleTimer = new Timer(200);
				sampleTimer.addEventListener(TimerEvent.TIMER, timer_audioSample);
				
				loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, app_uncaughtError);

			}
			
			private function app_uncaughtError(e:UncaughtErrorEvent):void
			{
				// Do something with your error.
				if (e.error is Error)
				{
					var error:Error = e.error as Error;
					LocaDebug.log("Uncaught error: " + error.name + " - " + error.message + " - " + error.getStackTrace());
				}
				else
				{
					var errorEvent:ErrorEvent = e.error as ErrorEvent;
					LocaDebug.log("Uncaught error event: " + errorEvent.errorID + " - " + errorEvent.text);
				}
			}
			
			public function user_update(user:LocachaUser):void
			{
				// if user has a display box, update the stream
				if(userCams[user.userID] && !user.videoStream)
				{
					mainLayout.removeElement(userCams[user.userID]);
					
					delete userCams[user.userID];	
					
					raiseSizeUpdate();
				}
				// no video box
				else if(!userCams[user.userID] && user.videoStream)
				{
					// good handle add
					var display:UserDisplay = new UserDisplay();
					display.setUser(this, user);
					
					mainLayout.addElement(display);
					
					var video:Video = new Video();
					video.width = 160;
					video.height = 120;
					video.attachNetStream(user.videoStream);
					display.video.addChild(video);

					userCams[user.userID] = display;
					
					raiseSizeUpdate();
				}
			}
			
			protected function application1_resizeHandler(event:ResizeEvent):void
			{
				raiseSizeUpdate();
			}
			
			public function raiseSizeUpdate():void
			{
				var rightHeight:Number = 0;
				
				if(mainLayout && mainLayout.visible)
					rightHeight = (mainLayout.layout as FlowLayout).internalHeight;
				
				if(rightHeight < 140)
					rightHeight = 140;

				if(rightHeight != lastHeight)
				{
					lastHeight = rightHeight;
					ExternalInterface.call("flash_sizeUpdate", rightHeight);
				}	
			}
			
			protected function turnOn():void
			{
				core.connect();
				mainLayout.visible = true;
				raiseSizeUpdate();
			}
			
			protected function turnOff():void
			{
				stopLocalVideo();
				core.dispose();
				mainLayout.visible = false;
				raiseSizeUpdate();
			}

			private function showStartVideoButton():void
			{
				startVideoButton = new Button();
				startVideoButton.label="start my video";
				startVideoButton.labelPlacement = "center";
				startVideoButton.width = 160;
				startVideoButton.height = 120;
				startVideoButton.addEventListener(MouseEvent.CLICK, startVideoButton_click);
				
				mainLayout.addElementAt(startVideoButton, 0);
			}
			
			public function stopLocalVideo():void
			{
				if(!localDisplay)
					return;
				
				camera = null;
				localDisplay.video.attachCamera(null);
				
				if(mic)
				{
					mic.addEventListener(SampleDataEvent.SAMPLE_DATA, mic_SampleData);
					sampleTimer.stop();
				}
				
				mainLayout.removeElement(localDisplay);
				mainLayout.addElementAt(startVideoButton, 0);
				
				core.updateLocalVideo(null, null);
			
				delete userCams[core.localUserID];
			}
			
			protected function startVideoButton_click(event:Event):void
			{
				try
				{
					camera = Camera.getCamera();
					
					if(camera)
					{
						mic = Microphone.getMicrophone();
						if(mic)
						{
							mic.addEventListener(SampleDataEvent.SAMPLE_DATA, mic_SampleData);
							sampleTimer.start()
						}
						
						camera.setMode(160, 120, 15);
						
						localDisplay = new UserDisplay();
						localDisplay.setUser(this, core.localUser);
						
						mainLayout.removeElement(startVideoButton);
						mainLayout.addElementAt(localDisplay, 0);
	
						localDisplay.video.attachCamera(camera);
	
						userCams[core.localUserID] = localDisplay;
					}
					else
						Alert.show("no camera found :(");
					
					core.updateLocalVideo(camera, mic);
				}
				catch(err:Error)
				{
					LocaDebug.logError("startVideoButton_click", err);
				}
			}

			protected function mainLayout_clickHandler(event:MouseEvent):void
			{
				selectUser("");
			}
			
			public function selectUser(userID:String):void
			{
				for each(var user:UserDisplay in userCams)
					user.highlight(userID);		
					
				if (ExternalInterface.available)
					ExternalInterface.call("flash_selectedUser", userID);		
			}
			
			private function mic_SampleData(event:SampleDataEvent):void
			{
				if(!localDisplay)
					return;
				
				// when the user pulls the slider up and down, they'll see their max volume be at the slider max
				
				var relativeVolume:Number = mic.activityLevel * mic.gain / 100;
				
				localDisplay.updateVolume(relativeVolume);
				
				lastSample = mic.activityLevel;
			}

			// basically there's no way for the receiver to query the audio volume coming in, so we have to send 
			// the levels manually. it doesnt have to match up perfectly or be on time, just something so the remote
			// user knows someone is talking on the other end
			private function timer_audioSample(event:TimerEvent) : void
			{
				try
				{
					sendSamples.push(lastSample);
					
					if(sendSamples.length == 10)
					{
						if(core.videoStream && core.localConnectState == ConnectState.CONNECTED)
							core.videoStream.send("audioSamples", sendSamples);
					
						sendSamples.length = 0;
					}	
				}
				catch(err:Error)
				{
					LocaDebug.logError("timer_audioSample", err);
				}
			}
			
		]]>
	</fx:Script>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<s:Group id="mainLayout" width="100%" height="100%" contentBackgroundAlpha="0" click="mainLayout_clickHandler(event)">
		<s:layout>
			<my:FlowLayout/>
		</s:layout>
		<!--<mx:Button width="160" height="120" label="hello" />
		<mx:Button width="160" height="120" label="start" />
		<mx:Button width="160" height="120" label="test" />-->
	</s:Group>

</s:Application>
